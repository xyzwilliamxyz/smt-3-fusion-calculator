plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'shot'
}
apply from: '../quality/jacoco.gradle'

shot {
    applicationId = "com.think4software.smtfusioncalculator"
}

jacoco {
    toolVersion = '0.8.7'
}

android {
    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            storeFile file(properties.getProperty('storeFile'))
            keyAlias properties.getProperty('keyAlias')
            storePassword properties.getProperty('storePassword')
            keyPassword properties.getProperty('keyPassword')
        }
    }
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "com.think4software.smtfusioncalculator"
        minSdkVersion 22
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.karumi.shot.ShotTestRunner"
        testApplicationId "com.think4software.smtfusioncalculator.test"
        signingConfig signingConfigs.release
    }

    buildTypes {
        debug {
            minifyEnabled false
            testCoverageEnabled true
        }
        release {
            signingConfig signingConfigs.release
            shrinkResources = true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
    buildFeatures {
        viewBinding = true
    }
    lintOptions {
        disable 'RtlHardcoded','RtlCompat', 'RtlEnabled'
    }
    testOptions {
        animationsDisabled = true
    }
    packagingOptions {
        exclude 'META-INF/*'
    }
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

dependencies {

    implementation "io.insert-koin:koin-core:$koin_version"

    implementation "androidx.room:room-runtime:2.3.0"
    implementation "androidx.room:room-ktx:2.3.0"

    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_version"

    kapt "androidx.room:room-compiler:2.3.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "com.google.android.material:material:1.4.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.1"
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.1.0"

    testImplementation "io.mockk:mockk:1.12.0"
    testImplementation "junit:junit:4.13.2"
    testImplementation "io.insert-koin:koin-test:$koin_version"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.3"
    androidTestImplementation "androidx.test:core-ktx:1.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
}